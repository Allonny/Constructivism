# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Lines configured by zsh-newuser-install
HISTFILE=~/.cache/histfile
HISTSIZE=10000
SAVEHIST=10000
bindkey -e
# End of lines configured by zsh-newuser-install
# The following lines were added by compinstall
zstyle :compinstall filename '/home/allonny/.zshrc'

autoload -U up-line-or-beginning-search
zle -N up-line-or-beginning-search

autoload -U down-line-or-beginning-search
zle -N down-line-or-beginning-search
# compinit
# End of lines added by compinstall

alias get="sudo xbps-install -Su"
alias del="sudo xbps-remove -R"
alias upd="sudo xbps-install -Suy ; flatpak update -y ; flatpak remove --unused --delete-data -y"

source ~/.local/share/antigen/antigen.zsh

antigen bundle zsh-users/zsh-autosuggestions
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle jirutka/zsh-shift-select
antigen theme romkatv/powerlevel10k

antigen apply

autoload -U select-word-style
select-word-style bash

function () {
	emulate -L zsh -o no_aliases

	# Make sure that the terminal is in application mode when zle is active,
	# since only then values from $terminfo are valid.
	if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
		autoload -U add-zle-hook-widget

		function .zshrc::term-application-mode() {
			echoti smkx
		}
		add-zle-hook-widget zle-line-init .zshrc::term-application-mode

		function .zshrc::term-normal-mode() {
			echoti rmkx
		}
		add-zle-hook-widget zle-line-finish .zshrc::term-normal-mode
	fi

	# View squences in showkey -a
	# `seq` is a fallback for the case when terminfo is not available.
	local kcap seq widget
	for	kcap   seq        widget (                       # key name
		khome  '^[[H'     beginning-of-line              # Home
		khome  '^[OH'     beginning-of-line              # Home (in app mode)
		kend   '^[[F'     end-of-line                    # End
		kend   '^[OF'     end-of-line                    # End (in app mode)
		kdch1  '^[[3~'    delete-char                    # Delete
		kpp    '^[[5~'    up-line-or-history             # PageUp
		knp    '^[[6~'    down-line-or-history           # PageDown
		kcuu1  '^[[A'     up-line-or-beginning-search    # UpArrow - fuzzy find history backward
		kcuu1  '^[OA'     up-line-or-beginning-search    # UpArrow - fuzzy find history backward (in app mode)
		kcud1  '^[[B'     down-line-or-beginning-search  # DownArrow - fuzzy find history forward
		kcud1  '^[OB'     down-line-or-beginning-search  # DownArrow - fuzzy find history forward (in app mode)
		kcbt   '^[[Z'     reverse-menu-complete          # Shift + Tab
		x      '^[[2;5~'  copy-region-as-kill            # Ctrl + Insert
		kDC5   '^[[3;5~'  kill-word                      # Ctrl + Delete
		kCBS   '^H'       backward-kill-word             # Ctrl + Backspace
		kRIT5  '^[[1;5C'  forward-word                   # Ctrl + RightArrow
		kLFT5  '^[[1;5D'  backward-word                  # Ctrl + LeftArrow
		kCABS  '^[^H'     clear-screen                   # Ctrl + Alt + Backspace


	); do
		bindkey -M emacs ${terminfo[$kcap]:-$seq} $widget
		bindkey -M viins ${terminfo[$kcap]:-$seq} $widget
		bindkey -M vicmd ${terminfo[$kcap]:-$seq} $widget
	done
}

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.local/share/p10k/p10k.zsh ]] || source ~/.local/share/p10k/p10k.zsh
